//===========================================================================
NAME          = "Scan PICODAS file and create a default template."
#include <version.gxh>
DESCRIPTION   = "\r\n\
\r\n\
    SCANPICO.DATA     : Picodas data file name\r\n\
            .TEMPLATE : Template file name (.I1 assumed)\r\n\
\r\n"

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Database/Import/Picodas PDAS"

//===========================================================================
//                                 RESOURCES
//===========================================================================

RESOURCE = "scanpico.gr"
#include "scanpico.grh"

//===========================================================================
//                                  INCLUDE
//===========================================================================

#include <all.gxh>     // system

//===========================================================================
//                                 VARIABLES
//===========================================================================

DGW         Diag;             // Dialogue handle

int         i;                // Utility 

string(STR_FILE) sData;
string(STR_FILE) sTemplate;
string(STR_DEFAULT_LONG) sTemp;

//===========================================================================
//                                   CODE
//===========================================================================

{

   // --- Are we running interactively ? ---

   if (iInteractive_SYS())
   {

     // --- Create the Dialogue ---

     Diag = Create_DGW("SCANPICOForm");

     // --- Set any Defaults ---

     SetInfoSYS_DGW(Diag,_SCANPICOFORM_0,DGW_FILEPATH,"SCANPICO","DATA");     
     SetInfoSYS_DGW(Diag,_SCANPICOFORM_1,DGW_FILEPATH,"SCANPICO","TEMPLATE");     

     // --- Run the Dialogue ---

     i = iRunDialogue_DGW(Diag);
     if (i != 0) Cancel_SYS();    // He hit cancel


     // --- Get the Strings ---

     GetInfoSYS_DGW(Diag,_SCANPICOFORM_0,DGW_FILEPATH,"SCANPICO","DATA");    
     GetInfoSYS_DGW(Diag,_SCANPICOFORM_1,DGW_FILEPATH,"SCANPICO","TEMPLATE");    

     // --- Destroy the Dialogue ---

     Destroy_DGW(Diag);

   }

   // --- Get Parameters ---

   GetString_SYS("SCANPICO","DATA",sData);
   GetString_SYS("SCANPICO","TEMPLATE",sTemplate);

   // --- turn on progress ---

   Progress_SYS(1);
   Strcpy_STR(sTemp, _("Scanning: "));
   Strcat_STR(sTemp,sData);
   ProgName_SYS(sTemp,1);

   // --- import ---

   ScanPico_DU(sData,sTemplate);

   // --- progress off ---

   Progress_SYS(0);

}
