//===========================================================================
NAME          = "Apply a low-pass filter"
#include <version.gxh>

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Database Tools/Filters"

DESCRIPTION   = "\

Apply a Low-pass filter to a channel.

    LOWPASS.IN       : Name of the Input Channel
           .OUT      : Name of the Ouput Channel
           .CUTOFF   : Short Wavelength cutoff in fiducials.
           .FILTLEN  : Filter length in fiducials.  If 0 is specified,
                       a default length equal to the longest wavelength
                       will be used.
"

//===========================================================================

RESOURCE = "lowpass.gr"
#include "lowpass.grh"

//===========================================================================

#include <all.gxh>

//===========================================================================
//                             GLOBAL VARIABLES
//===========================================================================

string(STR_DB_SYMBOL) sInCh,sOutCh;      // Channel Names

EDB        EData;
DB         Data;
DB_SYMB    InCh,OutCh;        // Channel handles
DB_SYMB    Line;              // Line handle
DGW        Diag;              // Dialogue handle
LST        List;              // List handle
real       rShortW;           // Short Wavelength cutoff values
int        i;                 // utility int
int        iLine;             // Number of Lines Processed
int        iTotLines;         // Total Number of Lines to Process

string(STR_DEFAULT_SHORT) sTemp;            // temporary number formating string
string(STR_DEFAULT) sLabel;

//===========================================================================
//                            Filter CORRECTION CODE
//===========================================================================

{

   // --- Get the Database ---

   EData = Current_EDB();
   Data = Lock_EDB(EData);

   // --- Are we running interactively ? ---

   if (iInteractive_SYS())
   {

      // --- Create the Dialogue ---

      Diag = Create_DGW("LOWPASSForm");


      // --- Set up lists ---

      List = GetList_DGW(Diag,_LOWPASSFORM_0);
      SymbLST_DB(Data,List,DB_SYMB_CHAN);
      Sort_LST(List,0,0);
      List = GetList_DGW(Diag,_LOWPASSFORM_1);
      SymbLST_DB(Data,List,DB_SYMB_CHAN);
      Sort_LST(List,0,0);

      // --- Set any Defaults from INI ---

      SetInfoSYS_DGW(Diag,_LOWPASSFORM_0,DGW_TEXT,"LOWPASS","IN");     
      SetInfoSYS_DGW(Diag,_LOWPASSFORM_1,DGW_TEXT,"LOWPASS","OUT");     
      SetInfoSYS_DGW(Diag,_LOWPASSFORM_2,DGW_TEXT,"LOWPASS","CUTOFF");    

      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // He hit cancel


      // --- Get the Strings and replace in INI ---

      GetInfoSYS_DGW(Diag,_LOWPASSFORM_0,DGW_TEXT,"LOWPASS","IN");     
      GetInfoSYS_DGW(Diag,_LOWPASSFORM_1,DGW_TEXT,"LOWPASS","OUT");     
      GetInfoSYS_DGW(Diag,_LOWPASSFORM_2,DGW_TEXT,"LOWPASS","CUTOFF"); 

      // --- set the filter length to default 0 ---
     
      SetInt_SYS("LOWPASS","FILTLEN",0);

      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);

   }

   // --- Get Parameters ---

   GetString_SYS("LOWPASS","IN",sInCh);
   GetString_SYS("LOWPASS","OUT",sOutCh);
   rShortW = rGetReal_SYS("LOWPASS","CUTOFF");


   // --- Does the Input Channel Exist ? ---

   if (!iExistSymb_DB(Data,sInCh,DB_SYMB_CHAN))  
      Abort_SYS(_("Input channel does not exist."));
   InCh = FindSymb_DB(Data,sInCh,DB_SYMB_CHAN);
   

   // --- Does the Ouput Channel Exist ? ---

   if (iExistSymb_DB(Data,sOutCh,DB_SYMB_CHAN) == 0)
      DupSymbNoLock_DB(Data,InCh,sOutCh);       // Create it 
   OutCh = FindSymb_DB(Data,sOutCh,DB_SYMB_CHAN);


   // --- Lock the channel symbols ---

   if (InCh != OutCh)
      LockSymb_DB(Data,InCh,DB_LOCK_READONLY,DB_WAIT_INFINITY);
   LockSymb_DB(Data,OutCh,DB_LOCK_READWRITE,DB_WAIT_INFINITY);


   // --- Prepare to do the work ---

   iLine = 0;
   iTotLines = iCountSelLines_DB(Data);
   Progress_SYS(1);


   // --- Go through all selected Lines ---

   ProgName_SYS("",1);
   Line = FirstSelLine_DB(Data);
   while (iIsLineValid_DB(Data,Line))
   {


      // --- Update the Progress Bar ---

      LockSymb_DB(Data,Line,DB_LOCK_READONLY,DB_WAIT_INFINITY);
      GetSymbName_DB(Data,Line,sTemp);
      UnLockSymb_DB(Data,Line);
      Strcpy_STR(sLabel,_("Filter line: "));
      Strcat_STR(sLabel,sTemp);
      ProgName_SYS(sLabel,0);
      ProgUpdateL_SYS(iLine,iTotLines);


      // --- apply filter ---

      BPFilt_DU(Data,Line,InCh,OutCh,rShortW,0.0,0);


      // --- Advance to Next Line ---        

      Line = NextSelLine_DB(Data, Line);
      iLine++;

   }


   // --- add the maker ---

   EasyMakerSymb_DB(Data,OutCh,_("Low-pass filter..."),"LOWPASS;");


   // --- Unlock the channel symbol ---

   if (InCh != OutCh)
      UnLockSymb_DB(Data,InCh);
   UnLockSymb_DB(Data,OutCh);


   // --- done ---

   Progress_SYS(0);


   // --- Unlock Data ---

   UnLock_EDB(EData);

   // --- display the new channel ---

		LoadChan_EDB( EData, sOutCh );
}
