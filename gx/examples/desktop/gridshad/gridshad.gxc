//===========================================================================
NAME          = "GRIDSHAD"
#include <version.gxh>

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Grid and Image/Filters"

DESCRIPTION   = "
   Created a shaded releife image from a grid.

    GRIDSHAD.IN      input grid file name
            .OUT     output shaded relief grid
            .INC     illumination inclination
            .DEC     illumination declination
            .SCL     vertical scale factor, m/z unit.
"
// Revision History
// v6.3.1: Get gray-shade ITR file from the settings.
//===========================================================================

RESOURCE = "gridshad.gr"
#include "gridshad.grh"


//===========================================================================

#include <all.gxh>


//===========================================================================
//                             GLOBAL VARIABLES
//===========================================================================

DGW        Diag;              // Dialogue handle
int        i;                 // utility int

string(STR_FILE) sIn,sOut,sGray,sTemp;

real        rInc,rDec,rScl;

IMG hIMG;
ITR hITR;

//===========================================================================

{
   
   if (iInteractive_SYS())
   {

      // --- Create the Dialogue ---

      Diag = Create_DGW("GRIDSHAD");

   
      // --- Set any Defaults from INI ---

      SetInfoSYS_DGW(Diag,_GRIDSHAD_0,DGW_FILEPATH,"GRIDSHAD","IN");     
      SetInfoSYS_DGW(Diag,_GRIDSHAD_1,DGW_FILEPATH,"GRIDSHAD","OUT");    
      SetInfoSYS_DGW(Diag,_GRIDSHAD_2,DGW_TEXT,"GRIDSHAD","INC");     
      SetInfoSYS_DGW(Diag,_GRIDSHAD_3,DGW_TEXT,"GRIDSHAD","DEC");     
      SetInfoSYS_DGW(Diag,_GRIDSHAD_4,DGW_TEXT,"GRIDSHAD","SCL");     


      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // He hit cancel


      // --- Get the Strings and replace in INI ---

      GetInfoSYS_DGW(Diag,_GRIDSHAD_0,DGW_FILEPATH,"GRIDSHAD","IN");
      GetInfoSYS_DGW(Diag,_GRIDSHAD_1,DGW_FILEPATH,"GRIDSHAD","OUT");
      GetInfoSYS_DGW(Diag,_GRIDSHAD_2,DGW_TEXT,"GRIDSHAD","INC");
      GetInfoSYS_DGW(Diag,_GRIDSHAD_3,DGW_TEXT,"GRIDSHAD","DEC");
      GetInfoSYS_DGW(Diag,_GRIDSHAD_4,DGW_TEXT,"GRIDSHAD","SCL");

           
      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);

   }


   // --- Get Parameters ---

   GetString_SYS("GRIDSHAD","IN",sIn);
   GetString_SYS("GRIDSHAD","OUT",sOut);
   if (iExistReal_SYS("GRIDSHAD","INC"))
      rInc = rGetReal_SYS("GRIDSHAD","INC");
   else
      rInc = rDUMMY;
   if (iExistReal_SYS("GRIDSHAD","DEC"))
      rDec = rGetReal_SYS("GRIDSHAD","DEC");
   else
      rDec = rDUMMY;
   if (iExistReal_SYS("GRIDSHAD","SCL"))
      rScl = rGetReal_SYS("GRIDSHAD","SCL");
   else
      rScl = rDUMMY;

   
   // --- Check for null input and output file name strings ---

   if (!iStrlen_STR(sIn))
      Abort_SYS(_("Input Grid Not Specified!"));

   if (!iStrlen_STR(sOut))
      Abort_SYS(_("New Resultant Grid Not Specified!"));   

   if (!iStrcmp_STR(sIn,sOut,STR_CASE_TOLERANT))
      Abort_SYS(_("Names of input and output grids must be different"));


   // --- Shade the grid ---

   Progress_SYS(1);
   ProgName_SYS(_("Shading..."),1);
   GridShad_IMU(sIn,sOut,rInc,rDec,rScl);


   // --- Set the defaults ---

   SetReal_SYS("GRIDSHAD","INC",rInc);
   SetReal_SYS("GRIDSHAD","DEC",rDec);
   SetReal_SYS("GRIDSHAD","SCL",rScl);
   
   
   // --- Get the name of the grey-scale ITR ---
   
   Strcpy_STR(sGray, "lgray.tbl");
   if (iGlobal_SYS("MONTAJ.DEFAULT_SHADE_COLOUR",sTemp) == 0)
      Strcpy_STR(sGray, sTemp);
   
   
   // --- set default grey-scale ITR ---
   
   hIMG = CreateFile_IMG(GS_FLOAT,sOut,IMG_FILE_READORWRITE);
   hITR = CreateIMG_ITR(hIMG,sGray,ITR_ZONE_DEFAULT,rDUMMY);  // if no zone in ITR, defaults to EQUAL_AREA
   iSetDefITR_IMG(hIMG,hITR);
   Destroy_SYS(hITR);
   Destroy_SYS(hIMG);

   // --- done ---
   
   Progress_SYS(0);

   
   // --- Add this grid to the current project ---

   if (iInteractive_SYS())   
      iAddDocument_PROJ(sOut,"Grid",1);
      
}
