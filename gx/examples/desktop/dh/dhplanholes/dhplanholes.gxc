//===========================================================================
NAME          = "Plot drill holes to an existing plan plot"
#include <version.gxh>

TRANSLATIONDOMAIN = "drillhole"
TRANSLATIONFRIENDLYPATH = "GX/DH-Plot"

DESCRIPTION   = "
   Plot drill holes to an existing plan plot.

	DHPLANHOLES.JOB:  Optional job file (set each time).
"

//===========================================================================
//                                 RESOURCES
//===========================================================================

//===========================================================================
//                                 INCLUDES
//===========================================================================

#include <all.gxh>

//===========================================================================
// dialogs
//===========================================================================

//===========================================================================
//                                 VARIABLES
//===========================================================================

DH       Drill;
EDB      EData;
EMAP     EMap;
MAP      Map;

int  iMod,iPage;
string(STR_FILE) sDB, sJob, sJob2;



//===========================================================================
//                                 CODE
//===========================================================================

{

   // --- get database ---
   
   EData = Current_EDB();
   GetName_EDB(EData, sDB);
   Drill = Create_DH(sDB);

   EMap = Current_EMAP();
   Map = Lock_EMAP(EMap);
      

	// --- Get preset job name, if supplied ---

	GetString_SYS("DHPLANHOLES", "JOB", sJob);
	SetString_SYS("DHPLANHOLES", "JOB", "");
	if(iStrlen_STR(sJob)==0)
		Strcpy_STR(sJob, "_plan.inp");


   // --- Are we running interactively ? ---

   iMod = 0;
   iPage = iDUMMY;
   if (iInteractive_SYS()) {


      // To modify a job, the job must be defined.

      if(iFileExist_SYS(sJob))
         OpenJob_DH(Drill, sJob, DH_PLOT_PLAN);
      else
         CreateDefaultJob_DH(Drill, sJob, DH_PLOT_PLAN);

      // --- Create a temporary copy of the job ---

      TempFileName_SYS(sJob,sJob2);
            
      if (iCopyFile_SYS(sJob, sJob2))
         Abort_SYS(_("Could not create temporary copy of job"));


      iPage = iGetInt_SYS("DHPLANHOLES", "PAGE");
      iMod = iModifyHoleTracesGUI_DH(Drill, sJob2, iPage);


      // --- Save the modified data ---

      if(iMod!=-1) {

         if (iCopyFile_SYS(sJob2, sJob))
            Abort_SYS(_("Could not restore from temporary copy of job"));
         
         SetInt_SYS("DHPLANHOLES", "PAGE", iPage);
            
      }            

      if(iFileExist_SYS(sJob2))
         iDeleteFile_SYS(sJob2);

      if(iMod == -1) Cancel_SYS();
   }

   

   // --- Plot the hole traces ---

   Progress_SYS(1);

   PlotHoleTraces_DH(Drill,Map,sJob);

   Progress_SYS(0);

   UnLock_EMAP(EMap);


   // --- Clean up ---

   Destroy_DH(Drill);


   // --- Bring the current map to the front ---

   MakeCurrent_EMAP(EMap);

}