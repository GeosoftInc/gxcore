//===========================================================================
NAME          = "Set display options for an array channel"
#include <version.gxh>
DESCRIPTION   = "\

Set profile colours for an array channel.

Parameters: 

   VADISPLAY.CHAN    VA Channel (N windows)
            .MINW    First window (0 to N-2)
            .MAXW    Last window (1 to N-1)
            .BASE    Base value
            .RANGE   Range of values displayed.
"

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Montaj Popups/Array Channels"

// Revision History:
// v5.1.5: New GX.
//===========================================================================
//                                 RESOURCES
//===========================================================================

RESOURCE = "VADISPLAY.gr"
#include "VADISPLAY.grh"

//===========================================================================
//                                  INCLUDE
//===========================================================================

#include <all.gxh>     // system

//===========================================================================
//                                 VARIABLES
//===========================================================================

EDB         EData;            // Database handle
DB          Data;             // Database handle
DB_SYMB     Ch;               // Channel Handle
DGW         Diag;             // Dialogue handle
LST         List;             // List handle

int i;

string(STR_DB_SYMBOL) sCh;
string(STR_DEFAULT) sMsg;

int iMinW, iMaxW;
real rBase, rRange;

//===========================================================================
//                                   CODE
//===========================================================================

{

   // --- Get database ---
   
   EData = Current_EDB();
   
   GetCurChan_EDB(EData,sCh);
   Ch = GetCurChanSymb_EDB(EData);      

   Data = Lock_EDB(EData);


   // --- Are we running interactively ? ---

   if (iInteractive_SYS())
   {

      // --- If current channel exists and is an array channel, set channel defaults. ---

      if(iStrlen_STR(sCh)) {

         LockSymb_DB(Data,Ch,DB_LOCK_READONLY,DB_WAIT_INFINITY);

         if(iGetColVA_DB(Data, Ch) > 1) {        
            SetString_SYS("VADISPLAY","CHAN",sCh);

            GetVAWindows_DB(Data, Ch, iMinW, iMaxW);
            GetVAScaling_DB(Data, Ch, rBase, rRange);

            SetInt_SYS("VADISPLAY","MINW",iMinW);
            SetInt_SYS("VADISPLAY","MAXW",iMaxW);
            SetReal_SYS("VADISPLAY","BASE",rBase);
            SetReal_SYS("VADISPLAY","RANGE",rRange);            
         }
   
         UnLockSymb_DB(Data, Ch);
      }



      // --- Create the Dialogue ---

      Diag = Create_DGW("VADISPLAY");

      
      // --- Set up lists ---

      List = GetList_DGW(Diag,_VADISPLAY_0);
      ArrayLST_DB(Data,List);
      Sort_LST(List,0,0);
     

      // --- Set any Defaults ---

      SetInfoSYS_DGW(Diag,_VADISPLAY_0,DGW_TEXT,"VADISPLAY","CHAN");
      SetInfoSYS_DGW(Diag,_VADISPLAY_1,DGW_TEXT,"VADISPLAY","MINW");
      SetInfoSYS_DGW(Diag,_VADISPLAY_2,DGW_TEXT,"VADISPLAY","MAXW");
      SetInfoSYS_DGW(Diag,_VADISPLAY_3,DGW_TEXT,"VADISPLAY","BASE");
      SetInfoSYS_DGW(Diag,_VADISPLAY_4,DGW_TEXT,"VADISPLAY","RANGE");
     

      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // He hit cancel


      // --- Get the Strings ---

      GetInfoSYS_DGW(Diag,_VADISPLAY_0,DGW_TEXT,"VADISPLAY","CHAN");
      GetInfoSYS_DGW(Diag,_VADISPLAY_1,DGW_TEXT,"VADISPLAY","MINW");
      GetInfoSYS_DGW(Diag,_VADISPLAY_2,DGW_TEXT,"VADISPLAY","MAXW");
      GetInfoSYS_DGW(Diag,_VADISPLAY_3,DGW_TEXT,"VADISPLAY","BASE");
      GetInfoSYS_DGW(Diag,_VADISPLAY_4,DGW_TEXT,"VADISPLAY","RANGE");
     

      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);
     
   }
   

   // --- Get Parameters ---

   GetString_SYS("VADISPLAY","CHAN",sCh);
   iMinW = iGetInt_SYS("VADISPLAY","MINW");
   iMaxW = iGetInt_SYS("VADISPLAY","MAXW");
   rBase = rGetReal_SYS("VADISPLAY","BASE");
   rRange = rGetReal_SYS("VADISPLAY","RANGE");


   if(!iStrlen_STR(sCh))
      Abort_SYS(_("VA channel not defined."));
         

   // --- Does the input channel exist ? ---

   Ch = FindChan_DB(Data, sCh);
   if(Ch==NULLSYMB)
      Abort_SYS(_("channel does not exist."));


   // --- Is it an array channel? ---

   LockSymb_DB(Data,Ch,DB_LOCK_READWRITE,DB_WAIT_INFINITY);

   if(iGetColVA_DB(Data, Ch)<2) {
      Printf_STR(sMsg, sizeof(sMsg), _("%s is not an array channel."), sCh);
      Abort_SYS(sMsg);
   }

   
   // --- Set the profile options ---

   SetVAWindows_DB(Data, Ch, iMinW, iMaxW);
   SetVAScaling_DB(Data, Ch, rBase, rRange);


   // --- Unlock ---

   UnLockSymb_DB(Data, Ch);
   UnLock_EDB(EData);

}
