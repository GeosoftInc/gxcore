//===========================================================================
NAME          = "Calculate statistics of marked data"
#include <version.gxh>

TRANSLATIONDOMAIN = "geochemistry"
TRANSLATIONFRIENDLYPATH = "GX/GeochemAnalysis"

DESCRIPTION   = "

Calculates statistics of the currently selected data in Oasis.

PARAMETERS:

     CHSTAT.CHANNEL  channel to calculate stats.
     CHSTAT.MASK     Mask channel to use. Blank for none.
"
// Revision History:
// v6.4.0: Fixed bug on showing stats subsequent times without changing channel.
// v6.0.0: Use exact statistics.
// v5.1.3: Using FindChan_DB and iExistChan_DB
//===========================================================================

RESOURCE = "chstat.gr"
#include "chstat.grh"

//===========================================================================

#include <all.gxh>

//===========================================================================
//                             GLOBAL VARIABLES
//===========================================================================

real        rItems, rDummies, rMin, rMax, rRange, rMean, rMedian, rSum;
real        rGeoMean, rSumSq, rVariance, rStdDev, rStdErr, rMode;
real        rSkewness, rKurtosis;
int         iLine,iTotLines,iStat,iMask;

ST          hST;
DGW         Diag,Report;

EDB         EData;
DB          Data;
DB_SYMB     Ch,MCh;
DB_SYMB     Line;
LST         List;
VV          VVd,VVm;

string(STR_DB_SYMBOL) sStr,sCh,sMask,sLastCh,sLastMask;
string(STR_DEFAULT_LONG) sTemp,sLabel;

//===========================================================================
//                              CODE
//===========================================================================

{
   // --- Report statistics ---

   if (!iInteractive_SYS())
      Abort_SYS(_("This GX will only run interactively."));
      

   // --- Get the Database ---

   EData = Current_EDB();
   Data = Lock_EDB(EData);


   // --- Get Default Mask Channel if required, and if it exists or is "None" ---

   if (iGlobal_SYS("CHIMERA.MASK",sMask) == 0) {
      if (iGlobal_SYS("CHIMERA.DEFMASK",sTemp) == 0) {
         if(iScanI_STR(sTemp)==1 &&
            (iExistChan_DB(Data,sMask) || 
             !iStrcmp_STR(sMask,_n("None"),STR_CASE_TOLERANT))) {

            SetString_SYS("CHSTAT","MASK",sMask);
         }
      }
   }


   // --- Create the Dialogue ---

   Diag = Create_DGW("CHSTAT");


   // --- Set up lists ---

   List = GetList_DGW(Diag,_CHSTAT_0);
   SymbLST_DB(Data,List,DB_SYMB_CHAN);
   Sort_LST(List,0,0);

   // --- Set up mask channel list ---

   List = GetList_DGW(Diag,_CHSTAT_1);
   MaskChanLST_CHIMERA(Data,List);
   Sort_LST(List,0,0);


   // --- calculate statistics ---

   hST = CreateExact_ST();
   
   
   // --- get some VV's ---

   VVd  = Create_VV(REAL,0);
   VVm = Create_VV(INT,0);


   Strcpy_STR(sLastCh,_n("NoChannel"));
   Strcpy_STR(sLastMask,_n("NoChannel"));
   
   do {

      // --- Set any Defaults from INI ---

      SetInfoSYS_DGW(Diag,_CHSTAT_0,DGW_TEXT,"CHSTAT","CHANNEL");     
      SetInfoSYS_DGW(Diag,_CHSTAT_1,DGW_TEXT,"CHSTAT","MASK");     


      // --- Run the Dialogue ---

      iStat = iRunDialogue_DGW(Diag);
      if (iStat == -1) Cancel_SYS();
      

      // --- Get the Strings and replace in INI ---

      GetInfoSYS_DGW(Diag,_CHSTAT_0,DGW_TEXT,"CHSTAT","CHANNEL");     
      GetInfoSYS_DGW(Diag,_CHSTAT_1,DGW_TEXT,"CHSTAT","MASK");     


      // --- get settings ---

      GetString_SYS("CHSTAT","CHANNEL",sCh);
      GetString_SYS("CHSTAT","MASK",sMask);

      if(iStrcmp_STR(sCh,sLastCh,STR_CASE_TOLERANT) ||
         iStrcmp_STR(sMask, sLastMask, STR_CASE_TOLERANT)) {


         // --- Does the Input Channel Exist ? ---

         if (!iExistChan_DB(Data,sCh))  
            Abort_SYS(_("Input channel does not exist."));
         Ch = FindChan_DB(Data,sCh);
         LockSymb_DB(Data,Ch,DB_LOCK_READONLY,DB_WAIT_NONE);


         // --- Apply mask? ---

         iMask = 0;
         if(iStrlen_STR(sMask)  && iStrcmp_STR(sMask, _n("None"), STR_CASE_TOLERANT)) {
            if (!iExistChan_DB(Data,sMask))  
               Abort_SYS(_("Mask channel does not exist."));
            MCh = FindChan_DB(Data,sMask);
            LockSymb_DB(Data,MCh,DB_LOCK_READONLY,DB_WAIT_NONE);     
            iMask = 1;
         }


         // --- Reset stats ---
      
         Reset_ST(hST);
      
      
         // --- Prepare to do the work ---

         iTotLines = iCountSelLines_DB(Data);
         Progress_SYS(1);


         // --- Go through all selected Lines ---

         ProgName_SYS(_("Accumulating statistics"),1);
         iLine = 0;
         Line = FirstSelLine_DB(Data);
         while (iIsLineValid_DB(Data,Line))
         {


            // --- Update the Progress Bar ---

            LockSymb_DB(Data,Line,DB_LOCK_READONLY,DB_WAIT_NONE);
            GetSymbName_DB(Data,Line,sTemp);
            UnLockSymb_DB(Data,Line);
            Strcpy_STR(sLabel,_("Accumulate stats on line: "));
            Strcat_STR(sLabel,sTemp);
            ProgName_SYS(sLabel,0);
            ProgUpdateL_SYS(iLine,iTotLines);


           // --- get data ---

           GetChanVV_DB(Data,Line,Ch,VVd);

            if(iMask) {

               GetChanVV_DB(Data,Line,MCh,VVm);
               ReFidVV_VV(VVm,VVd);
               Mask_VVU(VVd,VVm);

            }


            // --- stat ---

            Statistics_VV(hST,VVd);

            Line = NextSelLine_DB(Data, Line);
            iLine++;

         }
         Progress_SYS(0);


         // --- Unlock the channel symbols ---

         UnLockSymb_DB(Data,Ch);
         if(iMask)
            UnLockSymb_DB(Data,MCh);

         Strcpy_STR(sLastCh,sCh);
         Strcpy_STR(sLastMask,sMask);

      }
      
      
      // --- standard stats ---

      rItems   = rGetInfo_ST(hST, ST_ITEMS);
      rDummies = rGetInfo_ST(hST, ST_DUMMIES);
      rMin     = rGetInfo_ST(hST, ST_MIN); 
      rMax     = rGetInfo_ST(hST, ST_MAX);   
      rRange   = rGetInfo_ST(hST, ST_RANGE);
      rMean    = rGetInfo_ST(hST, ST_MEAN);  
      rStdDev  = rGetInfo_ST(hST, ST_STDDEV);


      // --- Create the Dialogue ---

      Report = Create_DGW("REPORT");


      // --- Set values in the dialogue ---
   
      SetInfo_DGW(Report,_REPORT_0,DGW_TEXT,sCh);

      FormatR_STR(rItems,sStr,20,19);
      SetInfo_DGW(Report,_REPORT_1,DGW_TEXT,sStr);

      FormatR_STR(rDummies,sStr,20,19);
      SetInfo_DGW(Report,_REPORT_2,DGW_TEXT,sStr);

      FormatR_STR(rMin,sStr,20,10);
      SetInfo_DGW(Report,_REPORT_3,DGW_TEXT,sStr);

      FormatR_STR(rMax,sStr,20,10);
      SetInfo_DGW(Report,_REPORT_4,DGW_TEXT,sStr);

      FormatR_STR(rRange,sStr,20,10);
      SetInfo_DGW(Report,_REPORT_5,DGW_TEXT,sStr);

      FormatR_STR(rMean,sStr,20,10);
      SetInfo_DGW(Report,_REPORT_6,DGW_TEXT,sStr);

      FormatR_STR(rStdDev,sStr,20,6);
      SetInfo_DGW(Report,_REPORT_7,DGW_TEXT,sStr);


      // --- display report ---

      iStat = iRunDialogue_DGW(Report);
      
      
      if (iStat == 2) {

         // --- More stats ---

         rItems    = rGetInfo_ST(hST, ST_NPOS);
         rGeoMean  = rGetInfo_ST(hST, ST_GEOMEAN);
         rSum      = rGetInfo_ST(hST, ST_SUM);
         rSumSq    = rGetInfo_ST(hST, ST_SUM2); 
         rVariance = rGetInfo_ST(hST, ST_VARIANCE);   
         rStdDev   = rGetInfo_ST(hST, ST_STDDEV);  
         rStdErr   = rGetInfo_ST(hST, ST_STDERR);
         rMean     = rGetInfo_ST(hST, ST_MEAN);
         rMedian   = rGetInfo_ST(hST, ST_MEDIAN);
         rMode     = rGetInfo_ST(hST, ST_MODE);
         rSkewness = rGetInfo_ST(hST, ST_SKEW);
         rKurtosis = rGetInfo_ST(hST, ST_KURTOSIS);


         // --- Create the Dialogue ---

         Report = Create_DGW("REPORT2");


         // --- Set values in the dialogue ---
      
         SetInfo_DGW(Report,_REPORT2_0,DGW_TEXT,sCh);

         FormatR_STR(rItems,sStr,20,19);
         SetInfo_DGW(Report,_REPORT2_1,DGW_TEXT,sStr);

         FormatR_STR(rGeoMean,sStr,20,10);
         SetInfo_DGW(Report,_REPORT2_2,DGW_TEXT,sStr);

         FormatR_STR(rSum,sStr,20,17);
         SetInfo_DGW(Report,_REPORT2_3,DGW_TEXT,sStr);

         FormatR_STR(rSumSq,sStr,20,17);
         SetInfo_DGW(Report,_REPORT2_4,DGW_TEXT,sStr);

         FormatR_STR(rVariance,sStr,20,6);
         SetInfo_DGW(Report,_REPORT2_5,DGW_TEXT,sStr);

         FormatR_STR(rStdDev,sStr,20,6);
         SetInfo_DGW(Report,_REPORT2_6,DGW_TEXT,sStr);

         FormatR_STR(rStdErr,sStr,20,10);
         SetInfo_DGW(Report,_REPORT2_7,DGW_TEXT,sStr);

         FormatR_STR(rMean,sStr,20,10);
         SetInfo_DGW(Report,_REPORT2_8,DGW_TEXT,sStr);

         FormatR_STR(rMedian,sStr,20,10);
         SetInfo_DGW(Report,_REPORT2_9,DGW_TEXT,sStr);

         FormatR_STR(rMode,sStr,20,10);
         SetInfo_DGW(Report,_REPORT2_10,DGW_TEXT,sStr);

         FormatR_STR(rSkewness,sStr,20,6);
         SetInfo_DGW(Report,_REPORT2_11,DGW_TEXT,sStr);

         FormatR_STR(rKurtosis,sStr,20,6);
         SetInfo_DGW(Report,_REPORT2_12,DGW_TEXT,sStr);


         // --- display report ---

         iStat = iRunDialogue_DGW(Report);
      
      }
      
   } while (iStat != -1);


   // --- Destroy ---

   Destroy_SYS(Diag);
   Destroy_SYS(Report);
   Destroy_SYS(VVd);
   Destroy_SYS(VVm);

   Destroy_ST(hST);

   // --- Release the Database ---

   UnLock_EDB(EData);

   // --- cancel the GX to prevent adding it to a GS ---

   Cancel_SYS();

}
