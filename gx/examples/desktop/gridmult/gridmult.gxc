//===========================================================================
NAME          = "GRIDMULT"
#include <version.gxh>

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Grid and Image"

DESCRIPTION   = "

Multiply a grid by a constant value.

Parameters:

      GRIDMULT.IN    File name of input grid.
              .OUT   File name of output grid. 
              .MULT  Multiplier to apply 
"

//===========================================================================
// Revision History:
//
// v6.0.0   Add output grid to the grid explorer
//===========================================================================


RESOURCE = "gridmult.gr"
#include "gridmult.grh"

//===========================================================================

#include <all.gxh>

//===========================================================================
//                             GLOBAL VARIABLES
//===========================================================================

DGW        Diag;              // Dialogue handle

int        i;                 // utility int

string(STR_FILE)  sIn;             // Name of input grid 
string(STR_FILE)  sOut;            // Name of output grid 

string(STR_DEFAULT_SHORT) sMult; // Multiplier to apply 
real         rMult;           

IMG         IMGIN;            // Image for the input grid
IMG         IMGOUT;           // Image for the output grid
IEXP        IEXP0;            // Image expression to multiply by rMult


//===========================================================================
//                            GRID UTIL CODE
//===========================================================================

{

   
   if (iInteractive_SYS())
   {

      // --- Create the Dialogue ---

      Diag = Create_DGW("GRIDMULTForm");
   
      // --- Set any Defaults from INI ---

      SetInfoSYS_DGW(Diag,_GRIDMULTFORM_0,DGW_FILEPATH,"GRIDMULT","IN");     
      SetInfoSYS_DGW(Diag,_GRIDMULTFORM_1,DGW_FILEPATH,"GRIDMULT","OUT");     
      SetInfoSYS_DGW(Diag,_GRIDMULTFORM_2,DGW_TEXT,"GRIDMULT","MULT");    

      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // User hit cancel


      // --- Get the Strings and replace in INI ---

      GetInfoSYS_DGW(Diag,_GRIDMULTFORM_0,DGW_FILEPATH,"GRIDMULT","IN");     
      GetInfoSYS_DGW(Diag,_GRIDMULTFORM_1,DGW_FILEPATH,"GRIDMULT","OUT");     
      GetInfoSYS_DGW(Diag,_GRIDMULTFORM_2,DGW_TEXT,"GRIDMULT","MULT");    


      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);

   }

   // --- Get Parameters ---

   GetString_SYS("GRIDMULT","IN",sIn);
   GetString_SYS("GRIDMULT","OUT",sOut);
   GetString_SYS("GRIDMULT","MULT",sMult);


   // --- Check for null input and output file name strings ---

   if (!iStrlen_STR(sIn))
      Abort_SYS(_("Input Grid Not Specified!"));

   if (!iStrlen_STR(sOut))
      Abort_SYS(_("Output Grid Name Not Specified!"));
      
   if (!iStrlen_STR(sMult))
      Abort_SYS(_("Multiplier Not Specified!"));   
   rMult=rScanR_STR(sMult);
    
   
   Progress_SYS(1);

   ProgName_SYS(_("Creating Images..."),1); 

   // --- Create Images tied to the grids ---  
   
   IMGIN = CreateFile_IMG(GS_DOUBLE,sIn,IMG_FILE_READONLY);
   IMGOUT = CreateOutFile_IMG(GS_DOUBLE,sOut,IMGIN); 

   ProgName_SYS(_("Applying multiplier..."),1);

   // --- Apply multiplier ---

   IEXP0 = Create_IEXP();
   AddGrid_IEXP(IEXP0,IMGIN,"In");
   AddGrid_IEXP(IEXP0,IMGOUT,"Out");

   DoFormula_IEXP(IEXP0,"Out = In * $rMult;",1024);


   // --- Destroy the Images ---
   
   Destroy_IEXP(IEXP0);
   Destroy_IMG(IMGIN);
   Destroy_IMG(IMGOUT);
        

   // --- done ---

   Progress_SYS(0);
   

   // --- Add this grid to the current project ---

   if (iInteractive_SYS())   
      iAddDocument_PROJ(sOut,"Grid",1);
   
}
