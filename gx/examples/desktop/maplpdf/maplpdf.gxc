//===========================================================================
NAME          = "Draw base map using a user PDF"
#include <version.gxh>

TRANSLATIONDOMAIN = "core"
TRANSLATIONFRIENDLYPATH = "GX/Map Tools/Base Map/MAPPLOT Control File"

DESCRIPTION   = "

   Create a base map using a user-suplied PDF.
   
      MAPLPDF.PDF    PDF file name
             .DRAW   0 - draw to current map
                     1 - draw to a new map
                     2 - draw to a new map created from an MDF file.
             .MAP    new map name
             .MDF    MDF name from which to create a new map
             .CLEAN  If set to 0, will not call the Clean_MAP function
                     after processing, which deletes empty views.
                     (Default is 1).
             
   The PDF may optionally create a custom MDF, which can be used to create
   a map using DRAW option 2.             

"

//===========================================================================

RESOURCE = "maplpdf.gr"
#include "maplpdf.grh"

//===========================================================================

#include <all.gxh>


//===========================================================================

string(STR_FILE)  sMDF,sMap,sPDF;

EMAP  EMap;
MAP   Map;
DGW   Diag;
MAPL  Mapplot;
real  rTime;
int   iDraw, iClean;


//===========================================================================

{

   // --- Clean map by default ---
   
   iClean = iGetInt_SYS("MAPLPDF", "CLEAN");
   SetInt_SYS("MAPLPDF", "CLEAN", iDUMMY);     // must set every time
   if(iClean!=0) iClean = 1;  // clean by default

   if (iInteractive_SYS()) {
   
   
      // --- set default drawing mode ---
      
      if (iHaveCurrent_EMAP()) 
         SetString_SYS("MAPLPDF","DRAW","0");
      else if (!iExistInt_SYS("MAPLPDF","DRAW"))
         SetInt_SYS("MAPLPDF","DRAW", 1);
        
         
      // --- Create the Dialogue ---

      Diag = Create_DGW("MAPLPDF");
      SetInfoSYS_DGW(Diag,_MAPLPDF_0,DGW_FILEPATH,"MAPLPDF","PDF");
      SetInfoSYS_DGW(Diag,_MAPLPDF_1,DGW_LISTALIAS,"MAPLPDF","DRAW");
      if (iRunDialogue_DGW(Diag)) Cancel_SYS();
      GetInfoSYS_DGW(Diag,_MAPLPDF_0,DGW_FILEPATH,"MAPLPDF","PDF");
      GetInfoSYS_DGW(Diag,_MAPLPDF_1,DGW_LISTALIAS,"MAPLPDF","DRAW");
      Destroy_DGW(Diag);
      
      
      // --- get PDF name ---
      
      GetString_SYS("MAPLPDF","PDF",sPDF);
      
      
      // --- get old _basemap.con time and run the PDF ---

      rTime = rFileTime_SYS("_basemap.con");
      if (iRunPDF_SYS("",sPDF)) Cancel_SYS();
      rTime = rFileTime_SYS("_basemap.con") - rTime;
      
      
      if (iFileExist_SYS("_basemap.con") && (rTime > 0.0)) {
      
         // --- get MDF file name ---
         
         iDraw = iGetInt_SYS("MAPLPDF","DRAW");
         if (iDraw == 2) {
         
            // --- MDF file ---
         
            Diag = Create_DGW("MAPLMDF");
            SetInfoSYS_DGW(Diag,_MAPLMDF_0,DGW_FILEPATH,"MAPLPDF","MDF");
            SetInfoSYS_DGW(Diag,_MAPLMDF_1,DGW_FILEPATH,"MAPLPDF","MAP");
            if (iRunDialogue_DGW(Diag)) Cancel_SYS();
            GetInfoSYS_DGW(Diag,_MAPLMDF_0,DGW_FILEPATH,"MAPLPDF","MDF");
            GetInfoSYS_DGW(Diag,_MAPLMDF_1,DGW_FILEPATH,"MAPLPDF","MAP");
            Destroy_DGW(Diag);
            
         }         
      }      
      
   } else
      rTime = 1.0;
   
   
   // --- if we created a control file, draw a map ---

   if (iFileExist_SYS("_basemap.con") && (rTime > 0.0)) {
   

      // --- get map drawing mode ---   
      
      iDraw = iGetInt_SYS("MAPLPDF","DRAW");
      
   
      // --- if have a current map ---
      
      if (iDraw == 1) {
      
         if (iRunGX_SYS("newmap") != 0)
            Cancel_SYS();
         
      } else if (iDraw == 2) {
      
         GetString_SYS("MAPLPDF","MAP",sMap);
         GetString_SYS("MAPLPDF","MDF",sMDF);
         if (iLoaded_EMAP(sMap)) UnLoad_EMAP(sMap);
         Map = Create_MAP(sMap,MAP_WRITENEW);
         MDF_MVU(Map,sMDF,"base","data");
         Destroy_MAP(Map);
         Load_EMAP(sMap);
         
      }
      
      // --- create MDF file ---
      
      EMap = Current_EMAP();
      Map = Lock_EMAP(EMap);

      if(iExistView_MAP(Map,"*Data"))
         MapMDF_MVU(Map,"_basemap.mdf","*Data");
      else
         MDF_MVU(Map,"_basemap.mdf","base","data");
      

      // --- process ---

      Progress_SYS(1);   
      ProgName_SYS(_("Mapplot"),1);
      
      Mapplot = Create_MAPL("_basemap.con","base",0);
      Process_MAPL(Mapplot,Map);
      Destroy_MAPL(Mapplot);
      
      
      // --- Clean up empty groups from all views in map ---

      if(iClean)
         Clean_MAP(Map);
      
      
      // --- done ---

      UnLock_EMAP(EMap);
      
      Progress_SYS(0);   
      
   }      
   
}
