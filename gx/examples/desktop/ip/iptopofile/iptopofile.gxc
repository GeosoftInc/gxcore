//===========================================================================
NAME          = "Import Topography ASCII file"
#include <version.gxh>

TRANSLATIONDOMAIN = "ip"
TRANSLATIONFRIENDLYPATH = "GX/IP/Import/Topography"

DESCRIPTION   = "
               Import a Topography ASCII file to the current \"Z\" channel.
"
// Revision History:
// v5.1.7: Creates Topo channel if not present
// v5.1.3: Using "Current" Z channel
//===========================================================================
//                                 RESOURCES
//===========================================================================

RESOURCE = "IPTOPOFILE.gr"
#include "IPTOPOFILE.grh"

//===========================================================================
//                                  INCLUDE
//===========================================================================

#include <all.gxh>     // system

//===========================================================================
//                                 VARIABLES
//===========================================================================

EDB         EData;            // Database handle
DB          Data;             // Database handle
DGW         Diag;             // Dialogue handle
IP          Ip;               // IP handle
DB_SYMB     Ch;               // Channel;

int         i;                // Utility 

string(STR_FILE) sFile;
string(STR_DB_SYMBOL) sZCh;

//===========================================================================
//                                   CODE
//===========================================================================

{
   // --- get database ---
   
   EData = Current_EDB();
   
   Data = Lock_EDB(EData);


   if(iInteractive_SYS())
   {
      // --- Create the Dialogue ---

      Diag = Create_DGW("IPTOPOFILE");


      // --- Set any Defaults ---

      SetInfoSYS_DGW(Diag,_IPTOPOFILE_0,DGW_FILEPATH,"IP","TOPOFILE");

      // --- Run the Dialogue ---

      i = iRunDialogue_DGW(Diag);
      if (i != 0) Cancel_SYS();    // He hit cancel


      // --- Get the Strings ---

      GetInfoSYS_DGW(Diag,_IPTOPOFILE_0,DGW_FILEPATH,"IP","TOPOFILE");

      // --- Destroy the Dialogue ---

      Destroy_DGW(Diag);

   } 
      

   // --- Get Parameters ---

   GetString_SYS("IP","TOPOFILE",sFile);


   // --- Create Topo channel? ---

   Ch = FindChan_DB(Data, "Topo");
   if(Ch==NULLSYMB)
      Ch = CreateSymb_DB(Data,"Topo",DB_SYMB_CHAN,DB_OWN_SHARED,DB_CATEGORY_CHAN_DOUBLE);


   // --- Create the IP object ---

   Ip = Create_IP();

   // --- turn on progress ---

   Progress_SYS(1);
   ProgName_SYS(_("Importing topography CSV file"),1);

   // --- Import the CSV data ---

   ImportTopoCSV_IP(Ip, Data, sFile);

   // --- Create a maker (only for interactive sessions) ---

   if(iInteractive_SYS()) {

      Ch = FindSymb_DB(Data, "Topo", DB_SYMB_CHAN);

      LockSymb_DB(Data, Ch, DB_LOCK_READWRITE, DB_WAIT_INFINITY);

      EasyMakerSymb_DB(Data, Ch, _("Import Topography..."), "IP;");

      UnLockSymb_DB(Data, Ch);
   }

   // --- Get current Z channel ---

   GetXYZChan_DB(Data, DB_CHAN_Z, sZCh);


   // --- release database to EDB ---
   
   UnLock_EDB(EData);
   
   LoadChan_EDB(EData,sZCh);
   LoadChan_EDB(EData,"Topo");


   // --- progress off ---

   Progress_SYS(0);

   Destroy_IP(Ip);
}
